plugins {
    id 'java'
    id 'jacoco'
    id('com.diffplug.spotless') version '6.25.0'
}

group 'edu.austral.ingsis'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ['--enable-preview']
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    jvmArgs += ['--enable-preview']
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.11"
    reportsDirectory = layout.buildDirectory.dir('customJacocoReportDir')
}

jacocoTestReport {
    dependsOn test
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.85
            }
        }
    }
}

// Code formatting configuration
spotless {
    java {
        googleJavaFormat()
    }
}
